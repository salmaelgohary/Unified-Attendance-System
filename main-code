#include <SPI.h>
#include <MFRC522.h>
#include <Arduino.h>
#include <WiFi.h>
#include <HTTPClient.h>
#include <WiFiClientSecure.h>

#define RST_PIN 16 
#define SS_PIN 4 
#define BUZZER 15
#define RED_LED 5
#define GREEN_LED 17

MFRC522 mfrc522(SS_PIN, RST_PIN);
MFRC522::MIFARE_Key key;
MFRC522::StatusCode status;

int blockNum = 2;

byte bufferLen = 18;
byte readBlockData[18];

String card_holder_name;
const String sheet_url = "https://script.google.com/macros/s/AKfycby8TatlTKscFYD_QcjkNOQy2pwITWKG8IVSW6a4-PT7bxDZsbhrwsRS9hKtU_2lyOU/exec?name=";

#define WIFI_SSID "Your Wifi Name"
#define WIFI_PASSWORD "Your Password"

bool noCardShown = false;  // state flag to only flash once

void setup() {
  Serial.begin(9600);
  Serial.println();
  Serial.print("Connecting to AP");
  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print(".");
    delay(200);
  }
  Serial.println("");
  Serial.println("WiFi connected.");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
  Serial.println();
  pinMode(BUZZER, OUTPUT);
  pinMode(RED_LED, OUTPUT);
  pinMode(GREEN_LED, OUTPUT);
  SPI.begin();
}

void loop() {
  mfrc522.PCD_Init();

  //Check if a card is repeated
  if (!mfrc522.PICC_IsNewCardPresent()) { return;}
  
  // Check if the card was read properly
  if (!mfrc522.PICC_ReadCardSerial()) { 
    if (!noCardShown) { // only blink once
      digitalWrite(RED_LED, HIGH);
      delay(200);
      digitalWrite(RED_LED, LOW);
      noCardShown = true; // prevent repeated blinking
    }
    return; 
  }
  
  Serial.println();
  Serial.println(F("Reading last data from RFID..."));
  ReadDataFromBlock(blockNum, readBlockData);
  //mfrc522.PICC_DumpToSerial(&(mfrc522.uid));

  //check for an uninitialized card
  bool isEmpty = true;
  for (int i = 0; i < 16; i++) {
    if (readBlockData[i] != 0x00) {
      isEmpty = false;
      break;
    }
  }

  if (isEmpty) {
    digitalWrite(RED_LED, HIGH);
    delay(200);
    digitalWrite(RED_LED, LOW);
    return;
  }

  //card is initialized, Print the data read from block
  Serial.println();
  Serial.print(F("Last data in RFID:"));
  Serial.print(blockNum);
  Serial.print(F(" --> "));
  for (int j = 0; j < 16; j++) {
    Serial.write(readBlockData[j]);
  }
  Serial.println();

  //flash green LED
  digitalWrite(GREEN_LED, HIGH); // turn red LED ON (failed read)
  delay(200);
  digitalWrite(GREEN_LED, LOW);
  
  // Play a tone pattern. edit the Hz for different pitches
  tone(BUZZER, 1000); // 1000 Hz tone
  delay(200);
  noTone(BUZZER);
  delay(200);

  tone(BUZZER, 1200); // 1200 Hz tone
  delay(200);
  noTone(BUZZER);

  if (WiFi.status() == WL_CONNECTED) {
    WiFiClientSecure client;
    client.setInsecure();

    String raw_name = String((char*)readBlockData);
    raw_name.trim();

    // Replace spaces with %20 for URL encoding
    raw_name.replace(" ", "%20");

    card_holder_name = sheet_url + raw_name;

    card_holder_name.trim();
    Serial.println(card_holder_name);
    HTTPClient https;
    Serial.print(F("[HTTPS] begin...\n"));

    if (https.begin(client, (String)card_holder_name)) {
      Serial.print(F("[HTTPS] GET...\n"));
      // start connection and send HTTP header
      int httpCode = https.GET();
      // httpCode will be negative on error
      if (httpCode > 0) {
        // HTTP header has been sent and Server response header has been handled
        Serial.printf("[HTTPS] GET... code: %d\n", httpCode);
        // file found at server
      } else {
        Serial.printf("[HTTPS] GET... failed, error: %s\n", https.errorToString(httpCode).c_str());
      }
      https.end();
      delay(1000);
    } else {
      Serial.printf("[HTTPS] Unable to connect\n");
    }
  }
}


void ReadDataFromBlock(int blockNum, byte readBlockData[]) {
  for (byte i = 0; i < 6; i++) {
    key.keyByte[i] = 0xFF;
  }
  status = mfrc522.PCD_Authenticate(MFRC522::PICC_CMD_MF_AUTH_KEY_A, blockNum, &key, &(mfrc522.uid));
  if (status != MFRC522::STATUS_OK) {
    Serial.print("Authentication failed for Read: ");
    Serial.println(mfrc522.GetStatusCodeName(status));
    return;
  } else {
    Serial.println("Authentication success");
  }
  status = mfrc522.MIFARE_Read(blockNum, readBlockData, &bufferLen);
  if (status != MFRC522::STATUS_OK) {
    Serial.print("Reading failed: ");
    Serial.println(mfrc522.GetStatusCodeName(status));
    return;
  } else {
    Serial.println("Block was read successfully");
  }
}
