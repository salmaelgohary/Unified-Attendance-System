#include <SPI.h>
#include <MFRC522.h>
#include <Arduino.h>
#include <WiFi.h>
#include <HTTPClient.h>
#include <WiFiClientSecure.h>
#include <LiquidCrystal_I2C.h>
#include <Wire.h>
#include <time.h>

// -------------------- PIN DEFINITIONS --------------------
#define RST_PIN    16
#define SS_PIN      4
#define BUZZER     15
#define RED_LED     5
#define GREEN_LED  17

// -------------------- WIFI --------------------
#define WIFI_SSID      "Your Wifi Network"
#define WIFI_PASSWORD  "Your Wifi Password"

// -------------------- NTP --------------------
const char* ntpServer = "pool.ntp.org";
const long gmtOffset_sec = -5 * 3600;  
const int daylightOffset_sec = 3600;

// -------------------- RFID --------------------
MFRC522 mfrc522(SS_PIN, RST_PIN);
MFRC522::MIFARE_Key key;
MFRC522::StatusCode status;
int blockNum = 2;
byte bufferLen = 18;
byte readBlockData[18];

// -------------------- LCD --------------------
LiquidCrystal_I2C lcd(0x27, 16, 2);

// -------------------- GLOBALS --------------------
unsigned long previousMillis = 0;
String request_url;
const String sheet_url = "https://script.google.com/macros/s/AKfycbxIhBMfBmXLRpOG0SNFmyu0kvBxE61qDOzA_jPqJ9kMka8rwt20q_20Ns7tw1Ee0RqH/exec?name=";

// =======================================================
//                       SETUP
// =======================================================
void setup() {
  Serial.begin(9600);

  // WiFi
  Serial.println("\nConnecting to WiFi...");
  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print(".");
    delay(200);
  }
  Serial.println("\nWiFi connected");
  Serial.println(WiFi.localIP());

  // Time
  configTime(gmtOffset_sec, daylightOffset_sec, ntpServer);

  // LCD
  lcd.init();
  lcd.backlight();
  lcd.clear();
  lcd.print("Welcome");

  // I/O + RFID
  pinMode(BUZZER, OUTPUT);
  pinMode(RED_LED, OUTPUT);
  pinMode(GREEN_LED, OUTPUT);

  SPI.begin();
  mfrc522.PCD_Init();
}

// =======================================================
//                        LOOP
// =======================================================
void loop() {
  displayWelcomeScreen();
  if (readRFID()) {
    sendToServer(readBlockData);
  }
}

// =======================================================
//               TONE + BUZZER FUNCTIONS
// =======================================================
void scanTone() {
  tone(BUZZER, 1200, 100);
  delay(120);
  noTone(BUZZER);
}

void playSuccessTone() {
  tone(BUZZER, 1000, 150);
  delay(200);
  tone(BUZZER, 1400, 150);
  delay(200);
  noTone(BUZZER);
}

void playErrorTone() {
  tone(BUZZER, 400, 200);
  delay(250);
  tone(BUZZER, 400, 200);
  delay(250);
  noTone(BUZZER);
}

// =======================================================
//                     LCD SCREENS
// =======================================================

// Live welcome screen with time
void displayWelcomeScreen() {
  unsigned long currentMillis = millis();
  if (currentMillis - previousMillis < 1000) return;

  previousMillis = currentMillis;
  struct tm timeinfo;

  if (!getLocalTime(&timeinfo)) {
    lcd.setCursor(0, 1);
    lcd.print("Time not found ");
    return;
  }

  int hour = timeinfo.tm_hour;
  bool isPM = hour >= 12;
  if (hour > 12) hour -= 12;
  if (hour == 0) hour = 12;

  lcd.clear();
  lcd.print("Welcome");
  lcd.setCursor(0, 1);
  if (hour < 10) lcd.print("0");
  lcd.print(hour);
  lcd.print(":");
  if (timeinfo.tm_min < 10) lcd.print("0");
  lcd.print(timeinfo.tm_min);
  lcd.print(":");
  if (timeinfo.tm_sec < 10) lcd.print("0");
  lcd.print(timeinfo.tm_sec);
  lcd.print(isPM ? " PM" : " AM");
}

// Status / success message
void showStatusMessage(String name, const String &message) {
  name.replace("%20", " ");

  lcd.clear();
  lcd.print(name);
  lcd.setCursor(0, 1);
  lcd.print(message);

  digitalWrite(GREEN_LED, HIGH);
  delay(200);
  digitalWrite(GREEN_LED, LOW);
  playSuccessTone();

  // scrolling section
  int displayWidth = 16;
  String msg = message;
  unsigned long scrollInterval = 400;
  unsigned long lastScroll = millis();
  int scrollIndex = 0;

  unsigned long startTime = millis();
  while (millis() - startTime < 10000) {
    if (msg.length() <= displayWidth) {
      lcd.setCursor(0, 1);
      lcd.print(msg);
      delay(2500);
      break;
    }

    if (millis() - lastScroll >= scrollInterval) {
      lcd.setCursor(0, 1);

      if (scrollIndex + displayWidth <= msg.length()) {
        lcd.print(msg.substring(scrollIndex, scrollIndex + displayWidth));
      } else {
        lcd.print(msg.substring(scrollIndex) + " ");
      }

      scrollIndex++;
      if (scrollIndex > msg.length()) scrollIndex = 0;
      lastScroll = millis();
    }
  }
}

// Logging screen
void logging(String name) {
  name.replace("%20", " ");
  lcd.clear();
  lcd.print(name);
  lcd.setCursor(0, 1);
  lcd.print("Logging...");
}

// General unified error screen
void fastOrUninitializedError() {
  String msg = "Scanned too fast/uninitialized";

  lcd.clear();
  lcd.print("Error");
  lcd.setCursor(0, 1);
  lcd.print("Scan again");

  digitalWrite(RED_LED, HIGH);
  delay(200);
  digitalWrite(RED_LED, LOW);
  playErrorTone();

  // Scroll long message
  int displayWidth = 16;
  unsigned long scrollInterval = 400;
  unsigned long lastScroll = millis();
  int scrollIndex = 0;

  unsigned long start = millis();
  while (millis() - start < 8000) {
    if (millis() - lastScroll > scrollInterval) {
      lcd.setCursor(0, 1);
      if (scrollIndex + displayWidth <= msg.length()) {
        lcd.print(msg.substring(scrollIndex, scrollIndex + displayWidth));
      } else {
        lcd.print(msg.substring(scrollIndex) + " ");
      }
      scrollIndex++;
      if (scrollIndex > msg.length()) scrollIndex = 0;
      lastScroll = millis();
    }
  }
}

//General Error Message
void showErrorMessage() {
  lcd.home();
  lcd.clear();
  lcd.print("Error");
  lcd.setCursor(0, 1);
  lcd.print("Scan again");

  //flash red LED
  digitalWrite(RED_LED, HIGH);
  delay(200);
  digitalWrite(RED_LED, LOW);

  playErrorTone(); 

  delay(4000); // show for 4 seconds
}

// =======================================================
//                 RFID READ FUNCTION
// =======================================================
bool readRFID() {
  if (!mfrc522.PICC_IsNewCardPresent()) return false;
  if (!mfrc522.PICC_ReadCardSerial())  return false;

  memset(readBlockData, 0, sizeof(readBlockData));
  scanTone();
  ReadDataFromBlock(blockNum, readBlockData);

  // Check if block is empty
  bool isEmpty = true;
  for (int i = 0; i < 16; i++) {
    if (readBlockData[i] != 0x00) {
      isEmpty = false;
      break;
    }
  }
  if (isEmpty) {
    fastOrUninitializedError();
    mfrc522.PICC_HaltA();
    mfrc522.PCD_StopCrypto1();
    return false;
  }

  // Debug output
  Serial.println("\nLast data in RFID block:");
  for (int j = 0; j < 16; j++) {
    Serial.write(readBlockData[j]);
  }
  Serial.println();

  mfrc522.PICC_HaltA();
  mfrc522.PCD_StopCrypto1();
  return true;
}

// =======================================================
//               SERVER COMMUNICATION
// =======================================================
void displayStatusMessage(String raw_name, String payload) {
  if (payload.indexOf("Sign Out") != -1)
    showStatusMessage(raw_name, "Goodbye! Have a good day");
  else if (payload.indexOf("Sign In") != -1)
    showStatusMessage(raw_name, "Welcome, Please Log Work Type");
  else
    showStatusMessage(raw_name, "Status updated");
}

void sendToServer(byte readBlockData[]) {
  if (WiFi.status() != WL_CONNECTED) return;

  WiFiClientSecure client;
  client.setInsecure();

  String raw_name = String((char*)readBlockData);
  raw_name.trim();
  raw_name.replace(" ", "%20");

  logging(raw_name);

  request_url = sheet_url + raw_name + "&from=esp32";
  Serial.println(request_url);

  HTTPClient https;
  https.setTimeout(15000);

  if (https.begin(client, request_url)) {

    int httpCode = https.GET();
    noTone(BUZZER);

    // follow redirect
    if (httpCode == HTTP_CODE_MOVED_PERMANENTLY || httpCode == HTTP_CODE_FOUND) {
      String newUrl = https.getLocation();
      https.end();
      https.begin(client, newUrl);
      httpCode = https.GET();
    }

    if (httpCode > 0) {
      String payload = https.getString();
      displayStatusMessage(raw_name, payload);
    } else {
      showErrorMessage();
    }
    https.end();

  } else {
    showErrorMessage();
  }
}

// =======================================================
//                  RFID BLOCK READER
// =======================================================
void ReadDataFromBlock(int blockNum, byte readBlockData[]) {
  for (byte i = 0; i < 6; i++) key.keyByte[i] = 0xFF;

  status = mfrc522.PCD_Authenticate(MFRC522::PICC_CMD_MF_AUTH_KEY_A, blockNum, &key, &(mfrc522.uid));
  if (status != MFRC522::STATUS_OK) {
    Serial.print("Auth failed: ");
    Serial.println(mfrc522.GetStatusCodeName(status));
    return;
  }

  status = mfrc522.MIFARE_Read(blockNum, readBlockData, &bufferLen);
  if (status != MFRC522::STATUS_OK) {
    Serial.print("Read failed: ");
    Serial.println(mfrc522.GetStatusCodeName(status));
  }
}
