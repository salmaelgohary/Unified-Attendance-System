#include <SPI.h>
#include <MFRC522.h>
#include <Arduino.h>
#include <WiFi.h>
#include <HTTPClient.h>
#include <WiFiClientSecure.h>
#include <LiquidCrystal_I2C.h>
#include <Wire.h>
#include <time.h>

//Pins
#define RST_PIN 16
#define SS_PIN 4
#define BUZZER 15
#define RED_LED 5
#define GREEN_LED 17

//Wifi credentials 
#define WIFI_SSID "Unified"
#define WIFI_PASSWORD "Un1f1edwireless"

//NTP setup
const char* ntpServer = "pool.ntp.org";
const long  gmtOffset_sec = -5 * 3600; // EST
const int   daylightOffset_sec = 3600;

//RFID setup
MFRC522 mfrc522(SS_PIN, RST_PIN);
MFRC522::MIFARE_Key key;
MFRC522::StatusCode status;

//LCD setup
LiquidCrystal_I2C lcd(0x27, 16, 2); //Create LCD instance with I2C address 0x27, 16 columns, 2 rows

// Global Variables
unsigned long previousMillis = 0; // global for timing

int blockNum = 2;

byte bufferLen = 18;
byte readBlockData[18];

String request_url;
const String sheet_url = "https://script.google.com/macros/s/AKfycbxIhBMfBmXLRpOG0SNFmyu0kvBxE61qDOzA_jPqJ9kMka8rwt20q_20Ns7tw1Ee0RqH/exec?name=";

bool noCardShown = false;  // state flag to only flash once

void setup() {
  Serial.begin(9600);

  Serial.println();
  Serial.print("Connecting to AP");
  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print(".");
    delay(200);
  }
  Serial.println("");
  Serial.println("WiFi connected.");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());
  Serial.println();

  // Initialize NTP
  configTime(gmtOffset_sec, daylightOffset_sec, ntpServer);

  // Initialize LCD
  lcd.init();
  lcd.backlight();
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Welcome");

  //initialize pins and RFID
  pinMode(BUZZER, OUTPUT);
  pinMode(RED_LED, OUTPUT);
  pinMode(GREEN_LED, OUTPUT);
  SPI.begin();
  mfrc522.PCD_Init();
}

void loop() {
  displayWelcomeScreen();
  if (readRFID()) {       // only send to server if a new card was scanned
    sendToServer(readBlockData);
  }
}

//Initial scanning tone
void scanTone(){
  tone(BUZZER, 1200, 100); // 1200 Hz for 100 ms
  delay(120);
  noTone(BUZZER);
}

//Buzzer Melodies
void playSuccessTone() {
  tone(BUZZER, 1000, 150); // low tone
  delay(200);
  tone(BUZZER, 1400, 150); // higher tone
  delay(200);
  noTone(BUZZER);
}

void playErrorTone() {
  tone(BUZZER, 400, 200);
  delay(250);
  tone(BUZZER, 400, 200);
  delay(250);
  noTone(BUZZER);
}

// Function to display the default welcome screen with live time
void displayWelcomeScreen() {
  unsigned long currentMillis = millis();

  if (currentMillis - previousMillis >= 1000) {
    previousMillis = currentMillis;

    struct tm timeinfo;
    if (getLocalTime(&timeinfo)) {
      int hour = timeinfo.tm_hour;
      bool isPM = false;

      if (hour >= 12) {
        isPM = true;
        if (hour > 12) hour -= 12;
      } else if (hour == 0) {
        hour = 12;
      }

      lcd.home();
      lcd.clear();
      lcd.print("Welcome");
      lcd.setCursor(0, 1);
      if (hour < 10) lcd.print("0");
      lcd.print(hour);
      lcd.print(":");
      if (timeinfo.tm_min < 10) lcd.print("0");
      lcd.print(timeinfo.tm_min);
      lcd.print(":");
      if (timeinfo.tm_sec < 10) lcd.print("0");
      lcd.print(timeinfo.tm_sec);
      lcd.print(isPM ? " PM" : " AM");
    } else {
      lcd.setCursor(0, 1);
      lcd.print("Time not found ");
    }
  }
}

// Function to show approval message temporarily
void showStatusMessage(String name, const String &message) {
  // Decode %20 back to space for display
  name.replace("%20", " ");
  
  lcd.home();
  lcd.clear(); //////////////////////////////////////////////////////////////////////////////////////////////////////////////////
  lcd.print(name);
  lcd.setCursor(0, 1);
  lcd.print(message);

  // Flash green LED
  digitalWrite(GREEN_LED, HIGH);
  delay(200);
  digitalWrite(GREEN_LED, LOW);

  playSuccessTone();

  //Scrolling text

  // Scrolling parameters
  int displayWidth = 16;  // width of LCD line
  String msg = message;   
  unsigned long scrollInterval = 400; // ms between scroll steps
  unsigned long lastScroll = millis();
  int scrollIndex = 0;

  // Show message for 10s total
  unsigned long startTime = millis();
  while (millis() - startTime < 10000) {
    // If message fits, just print once
    if (msg.length() <= displayWidth) {
      lcd.setCursor(0, 1);
      lcd.print(msg);
      delay(2500); // just hold it
      break;
    }

    // Scrolling message
    if (millis() - lastScroll >= scrollInterval) {
      lcd.setCursor(0, 1);
      if (scrollIndex + displayWidth <= msg.length()) {
        lcd.print(msg.substring(scrollIndex, scrollIndex + displayWidth));
      } else {
        // pad end with spaces to scroll out
        lcd.print(msg.substring(scrollIndex) + " ");
      }
      scrollIndex++;
      if (scrollIndex > msg.length()) scrollIndex = 0; // loop scroll if desired
      lastScroll = millis();
    }
  }
}

// Function to show logging message
void logging(String name) {
  // Decode %20 back to space for display
  name.replace("%20", " ");
  
  lcd.home();
  lcd.clear(); 
  lcd.print(name);
  lcd.setCursor(0, 1);
  lcd.print("Logging...");
}

//failed scan
void showErrorMessage() {
  lcd.home();
  lcd.clear();
  lcd.print("Error");
  lcd.setCursor(0, 1);
  lcd.print("Scan again");

  //flash red LED
  digitalWrite(RED_LED, HIGH);
  delay(200);
  digitalWrite(RED_LED, LOW);

  playErrorTone(); 

  delay(4000); // show for 4 seconds
}

bool readRFID(){
  //Check if a card is repeated
  if (!mfrc522.PICC_IsNewCardPresent()) { return false;}
  
  // Check if the card was read properly
  if (!mfrc522.PICC_ReadCardSerial()) { return false; }

  memset(readBlockData, 0, sizeof(readBlockData));
  scanTone();

  Serial.println();
  Serial.println(F("Reading last data from RFID..."));
  ReadDataFromBlock(blockNum, readBlockData);
  //mfrc522.PICC_DumpToSerial(&(mfrc522.uid));

  //check for an uninitialized card
  bool isEmpty = true;
  for (int i = 0; i < 16; i++) {
    if (readBlockData[i] != 0x00) {
      isEmpty = false;
      break;
    }
  }

  if (isEmpty) {
    showErrorMessage();
    // Halt the card and stop encryption so next scan works
    mfrc522.PICC_HaltA();
    mfrc522.PCD_StopCrypto1();
    return false;
  }
  
  //card is initialized, Print the data read from block
  Serial.println();
  Serial.print(F("Last data in RFID:"));
  Serial.print(blockNum);
  Serial.print(F(" --> "));
  for (int j = 0; j < 16; j++) {
    Serial.write(readBlockData[j]);
  }
  Serial.println();

  // Halt the card and stop encryption so next scan works
  mfrc522.PICC_HaltA();
  mfrc522.PCD_StopCrypto1();

  return true;
}

void displayStatusMessage(String raw_name, String payload) {
  if (payload.indexOf("Sign Out") != -1) { //sign out was found in response text
    showStatusMessage(raw_name, "Goodbye! Have a good day");
  } else if (payload.indexOf("Sign In") != -1) { //sign in was found in response text
    showStatusMessage(raw_name, "Welcome, Please Log Work Type");
  } else {
    showStatusMessage(raw_name, "Status updated");
  }
}

void sendToServer(byte readBlockData[]){
  if (WiFi.status() == WL_CONNECTED) {
    WiFiClientSecure client;
    client.setInsecure();

    String raw_name = String((char*)readBlockData);
    raw_name.trim();

    // Replace spaces with %20 for URL encoding
    raw_name.replace(" ", "%20");
    logging(raw_name);

    //buid url to send to apps script
    request_url = sheet_url + raw_name + "&from=esp32";

    request_url.trim();
    Serial.println(request_url);

    HTTPClient https;
    https.setTimeout(15000);   
    Serial.print(F("[HTTPS] begin...\n"));

    if (https.begin(client, (String)request_url)) {
      
      Serial.print(F("[HTTPS] GET...\n"));
      // start connection and send HTTP header
      int httpCode = https.GET();
      
      noTone(BUZZER);

      //check is url request is redirecting to another location. if so, follow to other location
      if (httpCode == HTTP_CODE_MOVED_PERMANENTLY || httpCode == HTTP_CODE_FOUND) {
        // Google redirect (302 or 301)
        String newUrl = https.getLocation();
        Serial.println("Redirected to: " + newUrl);

        https.end(); // close previous connection
        https.begin(client, newUrl); // start new one at redirected URL
        httpCode = https.GET();
      }

      // if httpcode was successful, check the text request
      if (httpCode > 0) { 
        // HTTP header has been sent and Server response header has been handled
        Serial.printf("[HTTPS] GET... code: %d\n", httpCode);

        //get server's reply text (payload)
        String payload = https.getString(); 

        //debugging purposes
        // Serial.println("=== PAYLOAD START ===");
        // Serial.println(payload);
        // Serial.println("=== PAYLOAD END ===");

        displayStatusMessage(raw_name, payload);

        // file found at server
      } else {
        Serial.printf("[HTTPS] GET... failed, error: %s\n", https.errorToString(httpCode).c_str()); //connected to the server but GET request failed
        showErrorMessage();
      }
      https.end();
      delay(1000);
    } else {
      Serial.printf("[HTTPS] Unable to connect\n"); //couldn't connect to server
      showErrorMessage();
    }
  }
}

void ReadDataFromBlock(int blockNum, byte readBlockData[]) {
  for (byte i = 0; i < 6; i++) {
    key.keyByte[i] = 0xFF;
  }
  status = mfrc522.PCD_Authenticate(MFRC522::PICC_CMD_MF_AUTH_KEY_A, blockNum, &key, &(mfrc522.uid));
  if (status != MFRC522::STATUS_OK) {
    Serial.print("Authentication failed for Read: ");
    Serial.println(mfrc522.GetStatusCodeName(status));
    return;
  } else {
    Serial.println("Authentication success");
  }
  status = mfrc522.MIFARE_Read(blockNum, readBlockData, &bufferLen);
  if (status != MFRC522::STATUS_OK) {
    Serial.print("Reading failed: ");
    Serial.println(mfrc522.GetStatusCodeName(status));
    return;
  } else {
    Serial.println("Block was read successfully");
  }
}
